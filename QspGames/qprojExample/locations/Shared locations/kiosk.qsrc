# kiosk

!!WD:"SUB >= 15" for act - Will need balancing / playtesting

if $kioskloc = 'counter' and $args[0] ! 'counter': gt 'kiosk', 'counter'

if $ARGS[0] = 'start':
	if hour >= 14 and hour <= 16:
		*clr & cla
		menu_off = 1
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/city/shared/kiosk/kiosk21.jpg"></center>'
		'It is currently very busy and looks like you''ll have to wait to get to the counter.'
		
		act 'Leave': minut += 1 & gt $loc
		
		act 'Wait in line (20 Minutes)': minut += 20 & gt 'kiosk', 'counter'

		if exhibitionist_lvl > 1:
			act 'Flash both your tits and your pussy to jump the queue':
				*clr & cla
				minut += 1
				gs 'flash', 'full', 'outdoors', 5
				gs 'stat'
				
				act 'Go to the counter': gt 'kiosk', 'counter'
			end
		end

		if exhibitionist_lvl > 0:
			act 'Flash your pussy to jump the queue':
				*clr & cla
				minut += 1
				gs 'flash', 'pussy', 'outdoors', 5
				gs 'stat'
				
				act 'Go to the counter': gt 'kiosk', 'counter'
			end
		end

		if pcs_inhib >= 40:
			act 'Flash your tits to jump the queue':
				*clr & cla
				minut += 1
				gs 'flash', 'tits', 'outdoors', 5
				gs 'stat'
				
				act 'Go to the counter': gt 'kiosk', 'counter'
			end
		end

		if exhibitionist_lvl > 0:
			act 'Suggest a blowjob in exchange for jumping the queue':
				if cumloc[11] = 0:
					*clr & cla
					pcs_throat += 1
					gs 'npcgeneratec', 0, 'stranger from the post office', rand(19,45)
					gs 'boyStat', $npclastgenerated

					inhib_exp += rand(3,5)
					'<center><img <<$set_imgh>> src="images/shared/sex/blowjob/kiosk_blowjob.jpg"></center>'
					'A man agrees to your suggestion and lets you move in front of him.'

					gs 'arousal', 'bj', 10, 'sub'
					gs 'stat'
					
					act 'Cum on your face':
						*clr & cla
						'<center><img <<$set_imgh>> src="images/pc/body/cum/cumface/cumface' + rand(1,23) + '.jpg"></center>'
						'The man cums on your face.'
						spafinloc = 11
						gs 'cum_manage'

						gs 'arousal', 'end'
						gs 'stat'
						
						act 'Go to the counter': gt 'kiosk', 'counter'
					end

					act 'Cum inside your mouth':
						*clr & cla
						'<center><img <<$set_imgh>> src="images/locations/shared/postoffice/sex/cumlip.jpg"></center>'
						'The man cums inside your mouth.'
						spafinloc = 12
						gs 'cum_manage'

						gs 'arousal', 'end'
						gs 'stat'
						
						act 'Go to the counter': gt 'kiosk', 'counter'
					end
				else
					*clr & cla
					'<center><video autoplay loop src="images/locations/shared/postoffice/punch.mp4"></video></center>'	
					'The Man punches you in the gut and says, <font color="#0D42C0">"Go away bitch, your face is covered in sperm."</font> You stagger away from the kiosk and spend 2 hours curled up in a ball.'
					minut += 120
					gs 'pain', 4, 'tummy', 'hit'
					gs 'stat'
					
					act 'Recover':gt $loc
				end
			end
		end
	else
		gt 'kiosk', 'counter'
	end
end

if $ARGS[0] = 'counter':
	*clr & cla
	$kioskloc = 'counter'
	$menu_loc = 'kiosk'
	$menu_arg = 'counter'
	menu_off = 0
	gs 'stat'
	
	'<center><img <<$set_imgh>> src="images/locations/city/shared/kiosk/kiosk123.jpg"></center>'
	'<center><img WIDTH = 245 HEIGHT = 56 src="images/locations/city/shared/kiosk/ruch.jpg"></center>'
	if $message ! '':
		'<center>'+$message+'</center>'
		killvar '$message'
	else
		''
	end	
	*pl '<center><table align="center" width=80%>' 
	*pl '<center><table align="center">'
	*P FUNC ('$stock_item', 20, 'Cigarettes', 115, 'siga', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Fashion magazine', 150, 'fashmag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Computer magazine', 150, 'compmag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Biographical magazine', 150, 'biomag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Cooking magazine', 150, 'cookmag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Knitting magazine', 150, 'knitmag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Fitness magazine', 150, 'fitmag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Umbrella', 1000, 'umbrella', 'kiosk', 'counter', '', iif(bag > 0, 0, 1), 'You need a purse')
	'</table></center>'
	*pl

	act 'Leave': $kioskloc = '' & minut += 1 & gt $loc

	if money >= 100:
		act 'Buy a snack (100 <b>₽</b>) (0:05)':
			*clr & cla
			gs 'stat'
			minut += 5
			money -= 100
			pcs_health += 10
			pcs_mood += 20
			fat += 6
			pcs_energy += 20
			if pcs_hydra >= 100:
				pcs_hydra += 10
			else
				pcs_hydra += 20
			end
			cumspclnt = 2
			gs 'cum_cleanup'
			pcs_breath = 0
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/shared/food/food.jpg"></center>'
			'You enjoy a small tasty, but somewhat fattening, snack.'
			act 'return': gt 'kiosk', 'start'
		end
	else
		'You don''t have enough money to buy a snack.'
	end

	if money >= 40:
		act 'Buy some water (40 <b>₽</b>) (0:05)':
			*clr & cla
			frost = 1
			if alko > 0: alko -= 1
			minut += 5
			money -= 40
			pcs_health += 10
			pcs_energy += 4
			if pcs_hydra >= 100:
				pcs_hydra += 25
			else
				pcs_hydra += 50
			end
			cumspclnt = 2
			gs 'cum_cleanup'
			pcs_breath = 0
			gs 'cum_cleanup'
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/shared/brothel/barorderwater.jpg"></center>'
			'You enjoy a drink of water'
			act 'return': gt 'kiosk', 'start'
		end
	else
		'You don''t have enough money to buy some water.'
	end

	act 'View phone themes': $kioskloc = '' & gt 'kiosk', 'themes'
end

if $ARGS[0] =  'themes':
	*clr & cla
	gs 'stat'
	
	'<center><img <<$set_imgh>> src="images/locations/city/shared/kiosk/kiosk123.jpg"></center>'
	'<center><img WIDTH = 245 HEIGHT = 56 src="images/locations/city/shared/kiosk/ruch.jpg"></center>'
	'All themes cost 500 <b>₽</b>'
	
	if money >= 500:
		i = 1
		:phonethemeloop
		if i = 1: $phoneNameTemp = 'Bimbo'
		if i = 2: $phoneNameTemp = 'Aluminus'
		if i = 3: $phoneNameTemp = 'Bottlecapped'
		if i = 4: $phoneNameTemp = 'Galactic'
		if i = 5: $phoneNameTemp = 'Vectored'
		if i = 6: $phoneNameTemp = 'Veneer'
		if i = 7: $phoneNameTemp = 'Voidwalker'
		if i = 8: $phoneNameTemp = 'Voidwalker Red'
		if i = 9: $phoneNameTemp = 'Voidwalker Toxic'
		if i = 10: $phoneNameTemp = 'Gopnik'
		if i = 11: $phoneNameTemp = 'Sports'
		if i = 12: $phoneNameTemp = 'Succubus'
		if i = 13: $phoneNameTemp = 'Woodshop'
		if phonetheme[i] = 0: '<a href="exec: VIEW ''images/system/phone/previews/<<$phonetheme_name[i]>>.png''">Preview</a> <a href="exec: money -= 500 & phonetheme[<<i>>] = 1 & gt ''kiosk'', ''themes''">Buy</a> the <b><<$phoneNameTemp>></b> theme.'
		i += 1
		if i < 14: jump 'phonethemeloop'
		killvar '$phoneNameTemp'
	else
		'You don''t have enough money to buy a phone theme.'
	end

	act 'Return': gt 'kiosk', 'counter'
end

--- kiosk ---------------------------------

